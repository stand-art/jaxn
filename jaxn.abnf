;--------------------------------------------------------
; The JAXN grammar (http://github.com/stand-art/jaxn/)
;
; Based on the JSON grammar, see RFC 7159.
; See RFC 5234 for interpretation and core rules.
;--------------------------------------------------------

eol = <end-of-line>           ; End-of-line
eof = <end-of-file>           ; End-of-file

comment = c-line / c-block

c-line = c-begin-line *( c-char ) c-end-line

c-begin-line = %x2F.2F        ; //
c-end-line = eol / eof

c-block = c-begin-block *( c-no-asterisk / ( c-asterisk c-no-slash ) ) c-end-block

c-begin-block = %x2F.2A       ; /*
c-end-block = %x2A.2F         ; */

c-char = HTAB / %x20-10FFFF   ; Any HTAB or printable character

c-asterisk = %x2A             ; * asterisk

c-no-asterisk = eol / HTAB / %x20-29 / %x2B-10FFFF
                              ; Any eol, HTAB or printable character except *

c-no-slash = eol / HTAB / %x20-2E / %x30-10FFFF
                              ; Any eol, HTAB or printable character except /

ws = *(
          SP /                ; Space
          HTAB /              ; Horizontal tab
          eol /               ; Line ending
	  comment             ; Comment
      )

begin-array     = ws %x5B ws  ; [ left square bracket
begin-object    = ws %x7B ws  ; { left curly bracket
end-array       = ws %x5D ws  ; ] right square bracket
end-object      = ws %x7D ws  ; } right curly bracket
string-concat   = ws %x2B ws  ; + plus
name-separator  = ws %x3A ws  ; : colon
value-separator = ws %x2C ws  ; , comma
value-sep-opt   = [ value-separator ]

null  = %x6E.75.6C.6C         ; null
true  = %x74.72.75.65         ; true
false = %x66.61.6C.73.65      ; false

number = [ plus / minus ] ( nan / inf / hex / dec )

nan = %x4E.61.4E              ; NaN

inf = %x49.6E.66.69.6E.69.74.79
                              ; Infinity

hex = zero x 1*HEXDIG         ; 0xXXX...

dec = ( int [ frac0 ] / frac1 ) [ exp ]

decimal-point = %x2E          ; .

digit1-9 = %x31-39            ; 1-9

e = %x65 / %x45               ; e E
x = %x78 / %x58               ; x X

exp = e [ plus / minus ] 1*DIGIT

frac0 = decimal-point *DIGIT
frac1 = decimal-point 1*DIGIT

int = zero / ( digit1-9 *DIGIT )

plus = %x2B                   ; +
minus = %x2D                  ; -
zero = %x30                   ; 0

string = string-part *( string-concat string-part )

string-part = s-string / d-string / b-string

d-string = d-quote *( s-char / s-quote ) d-quote
s-string = s-quote *( s-char / d-quote ) s-quote

s-char = unescaped /
         escape (
             %x22 /           ; "    double quote    U+0022
             %x27 /           ; '    single quote    U+0027
             %x5C /           ; \    reverse solidus U+005C
             %x2F /           ; /    solidus         U+002F
             %x62 /           ; b    backspace       U+0008
             %x66 /           ; f    form feed       U+000C
             %x6E /           ; n    line feed       U+000A
             %x72 /           ; r    carriage return U+000D
             %x74 /           ; t    tab             U+0009
             %x76 /           ; v    vtab            U+000B
             %x75 4HEXDIG /   ; uXXXX                U+XXXX
             %x78 2HEXDIG /   ; xXX  byte value XX
             %x30 )           ; 0    nul             U+0000

escape = %x5C                 ; \
d-quote = %x22                ; "
s-quote = %x27                ; '

unescaped = %x20-21 / %x23-26 / %x28-5B / %x5D-10FFFF

b-string = dollar *b-value

b-value = b-part *( dot b-part )

b-part = *(2HEXDIG)

dot = %2E                     ; .
dollar = %x24                 ; $

array = begin-array [ value *( value-separator value ) value-sep-opt ] end-array

object = begin-object [ member *( value-separator member ) value-sep-opt ] end-object

member = key name-separator value

key = string / identifier

identifier = i-begin *i-continue

i-begin = ALPHA / %x5F
i-continue = i-begin / DIGIT

value = false / null / true / object / array / number / string

JAXN-text = ws value ws
